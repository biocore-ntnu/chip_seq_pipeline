from natsort import natsorted


if config.get("annotation_gff3", ""):

    rule merge_gencode_genes_exons:
        input:
            gene = "{prefix}/data/compute_tss/gene/annotation_no_duplicate_tss.bed",
            exon = "{prefix}/data/compute_tss/exon/annotation_no_duplicate_tss.bed"
        output:
            "{prefix}/data/gene_barcharts/merged.bed"
        run:
            names = "Chromosome Start End Name Type Strand".split()
            gene = pd.read_table(input.gene, sep="\t", header=None, names=names)
            exon = pd.read_table(input.exon, sep="\t", header=None, names=names)
            gene.Type = "gene"
            exon.Type = "exon"

            df = pd.concat([gene, exon])
            df.to_csv(output[0], sep="\t", index=False, header=False)

else:
    rule merge_ucsc_genes_exons:
        input:
            "{prefix}/data/annotation/refgene/gene.bed",
            "{prefix}/data/annotation/refgene/exon.bed"
        output:
            "{prefix}/data/gene_barcharts/merged.bed"
        shell:
            "cat <(tail -n +2 {input[0]}) <(tail -n +2 {input[1]}) > {output[0]}"


rule add_tss:
    input:
        "{prefix}/data/gene_barcharts/merged.bed"
    output:
        "{prefix}/data/gene_barcharts/merged_with_tss.bed"
    run:
        names = "Chromosome Start End Name Type Strand".split()
        df = pd.read_table(input[0], sep="\t", header=None, names=names)

        tss_pos = df.loc[(df.Strand == "+") & (df.Type == "gene")]

        tss_neg = df.loc[(df.Strand == "-") & (df.Type == "gene")]
        tss_neg.loc[:, "Start"] = tss_neg.End

        tss = pd.concat([tss_pos, tss_neg])
        tss.loc[:, "End"] = tss.Start
        tss.loc[:, "Type"] = "tss"


        tes_pos = df.loc[(df.Strand == "+") & (df.Type == "gene")]
        tes_pos.loc[:, "Start"] = tes_pos.End

        tes_neg = df.loc[(df.Strand == "-") & (df.Type == "gene")]

        tes = pd.concat([tes_pos, tes_neg])
        tes.loc[:,"Type"] = "tes"
        tes.loc[:, "End"] = tes.Start

        tes.loc[:, "End"] = tes.Start + config["barchart_tss_length"]
        tes.loc[:, "Start"] = tes.Start - config["barchart_tss_length"]

        tss.loc[:, "End"] = tss.Start + config["barchart_tss_length"]
        tss.loc[:, "Start"] = tss.Start - config["barchart_tss_length"]

        pd.concat([df, tss, tes]).to_csv(output[0], sep="\t", index=False)


rule gene_overlap_barcharts_data:
    input:
        outfile = "{prefix}/data/peaks/{cs_caller}/{group}.csv",
        genes = "{prefix}/data/gene_barcharts/merged_with_tss.bed"
    output:
        "{prefix}/data/gene_barcharts/{cs_caller}/{group}.csv"
    params:
        label = lambda w: w.group
    script:
        "../../scrips/find_peak_gene_overlaps.py"


rule collect_gene_overlap_data:
    input:
        expand("{{prefix}}/data/gene_barcharts/{{cs_caller}}/{group}.csv", group=natsorted(ss.Group.drop_duplicates()))
    output:
        "{prefix}/data/gene_barcharts/{cs_caller}/merged_group.csv"
    run:
        dfs = [pd.read_table(f, sep=" ", header=0, index_col=None) for f in input]
        df = pd.concat(dfs)
        df.to_csv(output[0], sep=" ")


rule graph_gene_overlap_barcharts:
    input:
        "{prefix}/data/gene_barcharts/{cs_caller}/merged_group.csv"
    output:
        "{prefix}/data/gene_barcharts/{cs_caller}_gene_overlap_barchart.png"
    script:
        "../../scripts/barchart.R"
