import re


rule design_matrix:
    input:
        config["sample_sheet"]
    output:
        design = "{prefix}/data/limma/design_matrix.txt",
        # contrasts = "{prefix}/data/limma/contrasts.txt"
    script:
        "../../scripts/create_design_matrix.R"


rule design_matrix_loo:
    input:
        "{prefix}/data/limma/design_matrix.txt"
    output:
        "{prefix}/data/limma/loo/{group}.txt"
    run:
        group = re.sub("_lo$", "", wildcards.group)

        # print(group, wildcards.group)

        df = pd.read_table(input[0], index_col=0, sep=" ")
        # print(df)
        df = df.drop(group)
        df.to_csv(output[0], sep=" ")


rule limma:
    input:
        e_values = "{prefix}/data/revoom/{caller}.e_values",
        weights = "{prefix}/data/revoom/{caller}.weights",
        design = config["design_matrix"] if config["design_matrix"] else "{prefix}/data/limma/design_matrix.txt"
    output:
        all_regions = "{prefix}/data/limma/{caller}_{contrast}.toptable",
        cutoff = "{prefix}/data/limma/{caller}_{contrast}_cutoff.toptable"
    script:
        "../../scripts/differential_analysis.R" # config["differential_analysis"] if config["differential_analysis"] else


rule remove_loo_sample:
    input:
        e_values = "{prefix}/data/revoom/{caller}.e_values",
        weights = "{prefix}/data/revoom/{caller}.weights",
    output:
        e_values = "{prefix}/data/revoom/loo/{group}_{caller}.e_values",
        weights = "{prefix}/data/revoom/loo/{group}_{caller}.weights",
    run:
        e = pd.read_table(input.e_values, sep=" ")
        w = pd.read_table(input.weights, sep=" ")

        sample = re.sub("_lo$", "", wildcards.group)

        e = e[[f for f in e.columns if splitext(basename(f))[0] != sample]]
        w = w[[f for f in w.columns if splitext(basename(f))[0] != sample]]

        e.to_csv(output.e_values, sep=" ")
        w.to_csv(output.weights, sep=" ")


rule limma_loo:
    input:
        e_values = "{prefix}/data/revoom/loo/{group}_{caller}.e_values",
        weights = "{prefix}/data/revoom/loo/{group}_{caller}.weights",
        design = "{prefix}/data/limma/loo/{group}.txt"
    output:
        all_regions = "{prefix}/data/limma/loo/{group}_{caller}_{contrast}.toptable",
        cutoff = "{prefix}/data/limma/loo/{group}_{caller}_{contrast}_cutoff.toptable"
    script:
        "../../scripts/differential_analysis.R"


rule split_toptable_into_plus_minus_logfc:
    input:
        "{prefix}/data/limma/loo/{group}_{caller}_{contrast}_cutoff.toptable"
    output:
        "{prefix}/data/limma/loo/{logfc}_{group}_{caller}_{contrast}_cutoff.toptable"
    run:
        df = pd.read_table(input[0], sep=" ")

        if df.empty:
            df.to_csv(output[0], sep=" ")
        elif wildcards.logfc == "above":
            df.loc[df.logFC > 0].to_csv(output[0], sep=" ")
        elif wildcards.logfc == "below":
            df.loc[df.logFC < 0].to_csv(output[0], sep=" ")
