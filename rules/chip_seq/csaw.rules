from utils.file_getters import correct_cs_files
from utils.helpers import get_if_not_empty


def filenames_to_string(chip, input):
    fnames = chip
    fnames.extend(input)
    r = "c('" + "', '".join(fnames) + "')"
    return r

_get_cs_files = lambda w: correct_cs_files(ss, w.prefix, "chip", "bam", config, None)
_get_cs_index_files = lambda w: [f + ".bai" for f in correct_cs_files(ss, w.prefix, "chip", "bam", config, None)]


rule csaw_count:
    input:
        chip_input =  _get_cs_files,
        indexes = _get_cs_index_files
    output:
        rdata = "{prefix}/data/peaks/csaw/count/count_table.rds",
        table = "{prefix}/data/peaks/csaw/count/count_table.csv", # for logging purposes mostly
    conda:
        "../../envs/csaw.yaml"
    script:
        "../../scripts/csaw_count.R"


rule csaw_normalization_factors:
    input:
        chip_input =  _get_cs_files,
        indexes = _get_cs_index_files
    output:
        "{prefix}/data/peaks/csaw/normfacs/normfacs.csv"
    conda:
        "../../envs/csaw.yaml"
    script:
        "../../scripts/csaw_count_normfacs.R"


rule csaw_design_matrix:
    output:
        "{prefix}/data/peaks/csaw/design.csv"
    params:
        groups = lambda _: list(ss.ix[ss.loc[ss.ChIP=="ChIP"].Name.drop_duplicates().index].Group)
    script:
        "../../scripts/csaw_design_matrix.R"


rule csaw_prepare:
    input:
        data = "{prefix}/data/peaks/csaw/count/count_table.rds",
        normfacs = "{prefix}/data/peaks/csaw/normfacs/normfacs.csv",
        design = get_if_not_empty(config, "csaw_design_matrix", "{prefix}/data/peaks/csaw/design.csv")
    output:
        "{prefix}/data/peaks/csaw/estimate_disp.RDS"
    conda:
        "../../envs/csaw.yaml"
    script:
        "../../scripts/csaw_estimate_disp.R"


rule csaw_analyze:
    input:
        data = "{prefix}/data/peaks/csaw/count/count_table.rds",
        normfacs = "{prefix}/data/peaks/csaw/normfacs/normfacs.csv",
        design = get_if_not_empty(config, "csaw_design_matrix", "{prefix}/data/peaks/csaw/design.csv"),
        estimate_disp = "{prefix}/data/peaks/csaw/estimate_disp.RDS"
    output:
        "{prefix}/data/peaks/csaw/{contrast}.raw"
    # params:
    #     lambda w, input: compute_edger_contrast_vector()
    conda:
        "../../envs/csaw.yaml"
    script:
        "../../scripts/csaw_analyze.R"
