from utils.file_getters import correct_cs_files

macs2_cmd = "macs2 callpeak  {config[macs2][broad]} {params.paired_end} {config[macs2][extra_flags]} -t {input.chip} -c {input.input} -n {params.prefix} && mv {params.outfile} {output[0]}"



rule run_macs:
    input:
        chip = lambda w: correct_cs_files(ss, w.prefix, "chip", "bed", config, w.group),
        input = lambda w: correct_cs_files(ss, w.prefix, "input", "bed", config, w.group)
    output:
        "{prefix}/data/peaks/macs2/{group}.csv"
    params:
        prefix="{prefix}/data/peaks/macs2/{group}",
        outfile="{prefix}/data/peaks/macs2/{group}_peaks.narrowPeak" if not config["macs2"]["broad"] else "{prefix}/data/peaks/macs2/{group}_peaks.broadPeak",
        paired_end = "--format BAMPE" if config["paired_end"] else ""
    conda:
        "../../envs/macs2.yaml"
    threads: 1
    shell:
        macs2_cmd


rule merge_macs_results:
    input:
        expand("{{prefix}}/data/peaks/macs2/{group}.csv", group=groups)
    output:
        "{prefix}/data/peaks/macs2/merged.bed"
    shell:
        "sort -k1,1 -k2,3n {input} | bedtools merge {output[0]}"


rule run_macs_loo:
    input:
        chip = lambda w: correct_cs_files(loo_ss, w.prefix, "chip", "bed", config, w.group),
        input = lambda w: correct_cs_files(loo_ss, w.prefix, "input", "bed", config, w.group)
    output:
        "{prefix}/data/peaks/macs2/loo/{group}.csv"
    params:
        prefix="{prefix}/data/peaks/macs2/loo/{group}",
        outfile="{prefix}/data/peaks/macs2/loo/{group}_peaks.narrowPeak" if not config["macs2"]["broad"] else "{prefix}/data/peaks/macs2/loo/{group}_peaks.broadPeak",
        paired_end = "--format BAMPE" if config["paired_end"] else ""
    conda:
        "../../envs/macs2.yaml"
    threads: 1
    shell:
        macs2_cmd
