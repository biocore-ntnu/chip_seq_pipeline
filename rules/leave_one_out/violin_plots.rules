import pandas as pd

from os.path import basename, splitext
from re import sub

rule add_loo_info_to_chip_over_input:
    input:
        "{prefix}/data/loo/chip_over_input/{logfc}_{group}_{caller}_{contrast}.ratios"
    output:
        "{prefix}/data/loo/chip_over_input/{logfc}_{group}_{caller}_{contrast}_lo_info.ratios"
    run:
        df = pd.read_table(input[0], sep=" ", index_col=0)
        names = {f: splitext(basename(f))[0] for f in df.columns}

        original_group = loo_ss.loc[loo_ss.Group == wildcards.group].OriginalGroup.iloc[0]

        samples = list(ss.loc[ss.Group == original_group].Name.drop_duplicates())
        correct_cols = [f for f in df.columns if names[f] in samples]
        df = df[correct_cols]
        df.columns = [names[f] for f in df.columns]

        mdf = pd.melt(df, var_name="Sample", value_name="Ratio")

        loo_sample = sub("_lo$", "", wildcards.group)
        mdf.insert(2, "LeftOut", (mdf.Sample == loo_sample).astype(int))
        mdf.insert(3, "Contrast", wildcards.contrast)
        mdf.insert(4, "Group", original_group)

        contrast_groups = wildcards.contrast.split("-")

        group_in_contrast = mdf.Group.isin(contrast_groups)
        mdf = mdf[group_in_contrast]

        mdf.to_csv(output[0], sep=" ", index=False)


rule merge_loo_info:
    input:
        lambda w:
        expand("{prefix}/data/loo/chip_over_input/{logfc}_{group}_{caller}_{contrast}_lo_info.ratios",
               group=loo_ss.loc[loo_ss.OriginalGroup == w.group].Group,
               prefix=prefix, contrast=contrasts, caller=w.caller, logfc=w.logfc)
    output:
        "{prefix}/data/loo/chip_over_input/merged_{logfc}_{group}_{caller}_lo_info.ratios"
    run:
        dfs = [pd.read_table(f, sep=" ") for f in input]
        df = pd.concat(dfs).reset_index(drop=True)

        df.to_csv(output[0], sep=" ", index=False)


rule violinplot:
    input:
        "{prefix}/data/loo/chip_over_input/merged_{logfc}_{group}_{caller}_lo_info.ratios"
    output:
        "{prefix}/data/loo/chip_over_input/merged_{logfc}_{group}_{caller}_lo_info.pdf"
    script:
        "../../scripts/violinplots.R"
